class Math {
    static int twoToThe;

    function void init() {
        let twoToThe = Array.new(16);
        let twoToThe[0] = 1;
        let twoToThe[1] = 2;
        let twoToThe[2] = 4;
        let twoToThe[3] = 8;
        let twoToThe[4] = 16;
        let twoToThe[5] = 32;
        let twoToThe[6] = 64;
        let twoToThe[7] = 128;
        let twoToThe[8] = 256;
        let twoToThe[9] = 512;
        let twoToThe[10] = 1024;
        let twoToThe[11] = 2048;
        let twoToThe[12] = 4096;
        let twoToThe[13] = 8192;
        let twoToThe[14] = 16384;
        let twoToThe[15] = 16384+16384;
        return;
    }
    function boolean bit(int x, int b){
        return ~((x & twoToThe[b]) = 0);
    }
    function int abs(int x){
        if (~(x<0)){
            return x;
        }
        else {
            return (-x) ;
        }
    }
    function int multiply(int x, int y){
        var int i, sum, shiftedX;
        let i = 0;
        let shiftedX = x;
        while (i < 16){
            if (Math.bit(y,i)){
                let sum = sum + shiftedX;
            }
            let i = i + 1;
            let shiftedX = shiftedX + shiftedX;
        }
        return sum;
    }

    function int divide(int x, int y){
        var int q, negx, negy, ret;
        let negx = x < 0;
        let negy = y < 0;
        if (x<0) {
            let x = Math.abs(x);
        }
        if (y<0){
            let y = Math.abs(y);
        }
        if (x<y) {
            return 0;
        }
        let q = Math.divide(x, y + y);
        if (x - (q + q) * y < y){
            let ret = (q + q);
        }
        else { 
            let ret = (q + q) + 1;
        }
        if (negx = negy){
            return ret;
        }
        else{
            return -ret;
        }
    }

    function int min(int x, int y){
        if (x < y){
            return x;
        }
        else {
            return y;
        }
    }

    function int max(int x, int y){
        if (x<y){
            return y;
        }
        else{
            return x;
        }
    }

    function int sqrt(int x) {
        var int i, y, t;
        let i = 7;
        while (~(i < 0)){
            
            let t = (twoToThe[i] + y) * (twoToThe[i] + y);

            if (~(t>x) & (t>0)) {
                let y = y + twoToThe[i];
            }
            let i = i-1;
        }
        return y;
    }
}